;+---------------------------------------------------------------------------
; RAMFOS
; Преобразовать введенную строку с датой в числа
;
; На входе:
;  de - строка
;
; 2013-11-01 Доработано vinxru (короче и сохраняет в ОЗУ)
;----------------------------------------------------------------------------

#if DATE_IN_RAM

parseDate:	lxi	b, v_header2_date ; 3

parseDate_1:	; Преобразование строки в число. Ошибка не контроллируется.
		call	strToHex ; 6

		; Сохранение
		mov	a, l ; 7
		stax	b ; 8
		inx	b ; 9
	
		; Если конец строки, выходим
		rc ; 10

		; Цикл
		mvi	a, (v_header2_date + 3) & 0FFh ; 12
		cmp	c ; 13
		jnz	parseDate_1 ; 16

		; Выходим с флагами Z, NC, что приведет к повтормому вводу даты
		ret ; 17

#else ;---------------------------------------------------------------------

parseDate:	; День
		call	strToHex ; 3
		mov	a, l ; 4
		sta	parseDate_r1+1 ; 7
		rc ; 8
		; Месяц
		call	strToHex ; 11
		mov	a, l ; 12
		sta	parseDate_r2+1 ; 15
		rc ; 16
		; Год
		call	strToHex ; 19
		mov	a, l ; 20
		sta	parseDate_r3+1 ; 23
		rc ; 24
		; Слишком много параметров
		jmp	xra_a_stc_ret ; 27

#endif